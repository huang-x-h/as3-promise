<?xml version="1.0" encoding="utf-8"?>
<s:Application xmlns:fx="http://ns.adobe.com/mxml/2009" 
			   xmlns:s="library://ns.adobe.com/flex/spark" 
			   xmlns:mx="library://ns.adobe.com/flex/mx">
	<fx:Script>
		<![CDATA[
			import com.dormouse.Promise;
			
			import mx.collections.ArrayList;
			import mx.collections.IList;
			import mx.controls.Alert;
			
			private function get(url:String):Promise {
				return new Promise(function(resolve:Function, reject:Function):void {
					var request:URLRequest = new URLRequest(url);
					var loader:URLLoader = new URLLoader(request);
					loader.addEventListener(Event.COMPLETE, function(e:Event):void {
						resolve(e.target.data);
					});
					loader.addEventListener(IOErrorEvent.IO_ERROR, function(e:IOErrorEvent):void {
						reject(e);
					});
				});
			}
			
			protected function button1_clickHandler(event:MouseEvent):void
			{
				var promise1:Promise = get('directory.json');
				var promise2:Promise = get('menu.json');
				var promise3:Promise = get('portal.json');
				
				Promise.all([promise1, promise2, promise3]).then(function(result:Array):void {
					gridDirectory.dataProvider = new ArrayList(JSON.parse(result[0]) as Array);
					gridMenu.dataProvider = new ArrayList(JSON.parse(result[1]) as Array);
					gridPortal.dataProvider = new ArrayList(JSON.parse(result[2]) as Array);
				}).error(function(reason:*):void {
					Alert.show(reason);
				});
			}
			
		]]>
	</fx:Script>
	
	<s:Panel title="Promise Example for load url content"
			  width="75%" height="75%">
		<s:VGroup left="10" right="10" top="10" bottom="10">
			<s:Button label="Load directory menu portal data" click="button1_clickHandler(event)"/>
			<s:DataGrid id="gridDirectory" width="100%" height="100%">
			</s:DataGrid>
			<s:DataGrid id="gridMenu" width="100%" height="100%">
			</s:DataGrid>
			<s:DataGrid id="gridPortal" width="100%" height="100%">
			</s:DataGrid>
		</s:VGroup>
	</s:Panel>
</s:Application>
